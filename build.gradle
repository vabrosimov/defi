plugins {
    id 'java'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.liquibase.gradle' version '2.2.2'
}

def versionProps = new Properties()
file("version.properties").withInputStream {
    versionProps.load(it)
}

version = versionProps["version"]
group = 'ru.abrosimov.defi'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    
    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    // Db
    runtimeOnly 'org.postgresql:postgresql:42.7.7'

    // Liquibase
    implementation 'org.liquibase:liquibase-core:4.24.0'

    liquibaseRuntime 'org.liquibase:liquibase-core:4.24.0'
    liquibaseRuntime 'org.liquibase:liquibase-groovy-dsl:4.0.1'
    liquibaseRuntime 'info.picocli:picocli:4.7.7'
    liquibaseRuntime 'org.postgresql:postgresql:42.7.7'
}

test {
    useJUnitPlatform()
}

liquibase {
    activities {
        main {
            changelogFile 'db/migrations/changelog.xml'
            url project.findProperty('dbUrl') ?: 'jdbc:postgresql://localhost:5432/defi'
            username project.findProperty('dbUsername') ?: 'defi_app'
            password project.findProperty('dbPassword') ?: '123456'
            changeLogParameters([
                    schemaName: 'defi',
                    appRole: 'defi_app'
            ])
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "nexus"
            url = version.endsWith("SNAPSHOT")
                    ? uri("http://95.174.94.249:8081/repository/maven-snapshots/")
                    : uri("http://95.174.94.249:8081/repository/maven-releases/")
            allowInsecureProtocol = true // отключаем https
            credentials {
                username = project.findProperty("NEXUS_USER")
                password = project.findProperty("NEXUS_PASSWORD")
            }
        }
    }
}